buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies { classpath 'com.synopsys.integration:common-gradle-plugin:1.1.1' }
}

plugins { id 'groovy' }

configurations {
    jaxws.extendsFrom api
}

project.ext.moduleName = 'com.synopsys.integration.coverity-common-api'
project.ext.javaUseAutoModuleName = 'true'

group = 'com.synopsys.integration'
version = '2019.6.1-SNAPSHOT'
description = 'A library of generated request/response classes for the Coverity SOAP API, plus manually created response classes for the Coverity REST API.'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

apply plugin: 'com.synopsys.integration.library'

nexusStaging {
    packageGroup = 'com.synopsys'
    stagingProfileId = '4af8bea8300633'
}

dependencies {
    // Dependencies required to replace the JAX WS classes removed from the JDK in version 11
    api 'com.sun.xml.ws:rt:2.3.2'
    api 'com.sun.xml.ws:jaxws-ri:2.3.2'
    api 'com.sun.xml.ws:jaxws-rt:2.3.2'
    api 'com.sun.org.apache.xml.internal:resolver:20050927'
}

task wsimport {
    def generationPath = project.sourceSets.main.java.srcDirs.first()
    ext.destDir = file(generationPath)

    doLast {
        if (project.hasProperty('coverityServerUrl')) {
            def wspackage = 'com.synopsys.integration.coverity.api.ws'
            String configurationServiceWsdlUrl = "$coverityServerUrl/ws/v9/configurationservice?wsdl"
            String defectServiceWsdlUrl = "$coverityServerUrl/ws/v9/defectservice?wsdl"

            ant {
                sourceSets.main.output.classesDirs.inits()
                destDir.mkdirs()
                taskdef(name: 'wsimport',
                        classname: 'com.sun.tools.ws.ant.WsImport',
                        classpath: configurations.jaxws.asPath)
                wsimport(keep: true,
                        sourcedestdir: generationPath,
                        package: "${wspackage}.configuration",
                        wsdl: configurationServiceWsdlUrl) {
                    xjcarg(value: "-XautoNameResolution")
                }
                wsimport(keep: true,
                        sourcedestdir: generationPath,
                        package: "${wspackage}.defect",
                        wsdl: defectServiceWsdlUrl) {
                    xjcarg(value: "-XautoNameResolution")
                }
            }
        } else {
            logger.warn('coverityServerUrl not specified, will not wsimport the request and response classes')
        }
    }
}

compileJava {
    dependsOn wsimport
}
